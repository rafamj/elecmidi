#reads the pattern from the eletribe
read

name Test
length 4

#parts. The first parameter indicates the part(s)
lastStep 1-4 16

#voiceAssign possible values: Mono1, Mono2, Poly1, Poly2  or 0,1,2,3
voiceAssign 1 Mono1
#sets the gate time of all steps in part 1
gateTime 1 70

#sets the gate time for the steps 7,9,10 
gateTime 1 @7,9-10 17
gateTime 1 @15 80 

velocity 1 90
velocity 1 @1,5,9,13 120

#pattern for part 1 *=on x=off _=tie X=xxxx
pattern 1   *_x* *x*x *x*x *x*x   X **** X 
#start at step 29
pattern 1 @29 ****
pattern 1 @33  *_x* X *x*x *x*x   X **** X 

#notes for part 1:
#A5=A, octave 5. 
#g is tied with A5 (see pattern above). g=G
#C^=C, one octave up. 
#Cv=C, one octave down. 
#D+= D sharp (- = flat)
notes 1     A1 g B    C^ D   D+ Cv  B A    C^ d e f  c d e f
notes 1 @33       C D   D+ Cv  B A    C^ d e f  c d 

transpose 1 @1-6 -3



gateTime 2-3 90
velocity 2-3 80

pattern 2-3 *___ X *___ X         X X ****  **** 

#d3af^, e3bg^ are chords
# = repeat the previous note or chord
notes 2-3 d3af^  = = =     e3bg^ = = =  
#@25 -> start inserting at step 25 
# =*7 is equivalent to = = =   = = = =
notes 2-3 @25 g4a^b =*7

transpose 2     1

gateTime 4 90
velocity 4 80
pattern 4      ____ ____ ____ ____ 
pattern 4  @17 ____ ____ ____ ____ 
pattern 4  @33 ____ ____ ____ ____ 
pattern 4  @49 ____ ____ ____ ____ 

notes 4       d2f^*16     e2g^*16
notes 4 @33   d2f^  =*15     e2g^ =*15
#prints the data to stdout
#print 1-4

#writes back the modified pattern into the electribe
write


#performance commands:

#mutes all parts except 1
mute 2-16 on
mute 1 off
write

start

#waits 15 steps (until the end of the pattern, step 16)
wait 15 

#unmutes part 2
mute 2 off
write

#again step 16
wait 16 
mute 3 off
write

wait 15 

stop
